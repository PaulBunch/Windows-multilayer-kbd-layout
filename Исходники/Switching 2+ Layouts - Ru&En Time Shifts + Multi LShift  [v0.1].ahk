; Данная программа является дополнением к комфортной русско-английской клавиатурной раскладке
; Версия 0.1 - июль 2023 г.
; Автор Bunch; e-mail: p.m.puchkov@ya.ru; GitHub: _____

; ================
; === ОПИСАНИЕ ===
; ================

; 1. К написанию данного скрипта активно привлекался ChatGPT, но пока он оказывается бесполезным.
; 2. (удалить) Примеры переключения языково раскладки от "древних" http://forum.script-coding.com/viewtopic.php?id=189
; 3. Код написан на AutoHotkey v2 для Windows 10, на других ОС не проверялся.
; 4. Скрипт ориентирован на работу с пользовательсками русской и английской раскладками клавиатуры KBDRU_bunch_mini и KBDUS_bunch_mini разработаными Bunch'ем.
;    В этих раскладках переназначены модификаторы RAlt, CapsLock и RWin (RCtrl) на F13, F14 и F15 соответственно.



#SingleInstance  Force ; Пропускает диалоговое окно и автоматически заменяет старый (уже запущенный) экземпляр



; =======================================
; === ПЕРЕКЛЮЧЕНИЕ ЯЗОКОВЫХ РАСКЛАДОК ===
; =======================================

; Однократное нажатие переключение на русскую языковую раскладку
; Двойное нажатие переключение языковых раскладок по кругу
; В некоторых языках (например, в китайском Shift используется как модификатор иероглифов, поэтому данная функция будет создавать сложности в использовании таких раскладок)
~LShift::
{
    static clickCount := 0 ; Переменная для подсчета нажатий кнопки   
    
    if A_PriorKey != "LShift"
        TimerCheck()

    KeyWait "LShift" ; Ожидаем, пока кнопка зажата

    if A_PriorKey = "LShift" { ; Нажимались ли какие-нибудь кнопки, пока LShift была зажата?

        clickCount += 1 ; Увеличиваем счетчик нажатий на 1

        switch clickCount {
            case 1: ; Таймер ещё не запущен
                SetTimer ResetCount, 200 ; Запускаем таймер [мс]
                return
            case 2: ; Переключить язык по кругу
                SetTimer ResetCount, 0 ; Останавливаем таймер. Если не остановить, то эта функция будет повторяться автоматически с заданным интервалом
                clickCount := 0 ; Обнуляем счетчик нажатий
                if WinExist("A")
                    PostMessage 0x0050, , , , "A" ; Последовательно переключить языковую раскладку активного окна
                return               
        }
        return
    }
    else
        TimerCheck()

    ; Проверка работы таймера
    TimerCheck() {
        if clickCount > 0 {
            SetTimer ResetCount, 0 ; Останавливаем таймер. Если не остановить, то эта функция будет повторяться автоматически с заданным интервалом
            clickCount := 0 ; Обнуляем счетчик нажатий 
        }
    }

    ; Сброс счетчика, остановка таймера и включение русской раскладки
    ResetCount() {              
        SetTimer ResetCount, 0 ; Останавливаем таймер. Если не остановить, то эта функция будет повторяться автоматически с заданным интервалом
        clickCount := 0 ; Обнуляем счетчик нажатий
        if A_PriorKey = "LShift" && WinExist("A")            
            PostMessage 0x50, 0, 0x0419, , "A" ; Отправка сообщения для установки Русской раскладки
    }
}

; Быстрое включение английской языковой раскладки
~RShift Up::
{
    if A_PriorKey = "RShift" && WinExist("A")
        PostMessage 0x50, 0, 0x0409, , "A" ; Отправка сообщения для установки Английской (США) раскладки
}



; =============================
; === ПЕРЕНАЗНАЧЕНИЕ КНОПОК ===
; =============================

; Включение третьего слоя (верхнего регистра) через комбинацию Shift + CapsLock (F14)
; CapsLock переназначен на F14
+F14::CapsLock



; ===============================
; === Слой 4 - слой навигации ===
; ===============================

; RAlt переназначен на F13

~F13 & j::Send "{Blind}{Left}"
~F13 & k::Send "{Blind}{Down}"
~F13 & l::Send "{Blind}{Right}"
~F13 & i::Send "{Blind}{Up}"

~F13 & h::Send "{Blind}{Backspace}"
~F13 & SC027::Send "{Blind}{Del}"

~F13 & e::Send "{Blind}{PgUp}"
~F13 & d::Send "{Blind}{PgDn}"
~F13 & s::Send "{Blind}{Home}"
~F13 & f::Send "{Blind}{End}"